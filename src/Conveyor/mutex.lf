target Python

import FrostBase from "../../../frost/src/lib/FrostBase.lf"

reactor Mutex extends FrostBase{
    state mutex_time = 2500
    state pallet_present = False
    //Physical links
    input from_switch 
    output to_switch

    //delay for the mutex
    logical action delay

    reaction(from_switch)->delay{=
        value = self._get_input_values(from_switch)

        if not isinstance(value, Pallet):
            raise Exception(f"Mutex {self.name} error: received non-pallet value from switch: {value}")

        self.logger.debug(f"Mutex {self.name} received from switch: {value}")

        if self.pallet_present:
            raise Exception(f"Mutex {self.name} error: trying to send a pallet when another is still in the mutex")
            
        self.pallet_present = True
        delay.schedule(MSEC(self.mutex_time), value)
    =}

    reaction(delay)-> to_switch{=
        self.pallet_present = False
        self.logger.debug(f"Mutex {self.name} sent to switch: {delay.value}")
        self._set_output_port(delay.value, to_switch)
    =}    
}